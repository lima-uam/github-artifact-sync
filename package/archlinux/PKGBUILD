# Maintainer: Antonio de Haro <antonio.deharo@lima.sh>

pkgname=github-artifact-sync
pkgver=0.1.4
pkgrel=1
pkgdesc='Service for syncing github workflow artifacts with webhooks'
arch=('x86_64')
url='https://github.com/lima-uam/github-artifact-sync'
license=('MIT OR Apache-2.0')
depends=(
    'gcc-libs'
    'glibc'
    'libssl.so'
    'libcrypto.so'
)
makedepends=('cargo')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('62db97a3a505ecfb119ccc5e33f90620c2c2dcc61c12aa562c51d299e04c8150')

prepare() {
    export RUSTUP_TOOLCHAIN=stable
    cd "${pkgname}-${pkgver}"

    cargo fetch --locked
}


build() {
    export RUSTUP_TOOLCHAIN=stable
    cd "${pkgname}-${pkgver}"

    cargo build --frozen --release
}

package() {
    cd "${pkgname}-${pkgver}"

    install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

    install -Dm644 package/systemd/github-artifact-sync.service "${pkgdir}/usr/lib/systemd/system/github-artifact-sync.service"
    install -Dm640 package/systemd/github-artifact-sync.conf "${pkgdir}/etc/github-artifact-sync.conf"

    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
    install -Dm644 LICENSE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
    install -Dm644 THIRD-PARTY-LICENSES.md "${pkgdir}/usr/share/licenses/${pkgname}/THIRD-PARTY-LICENSES.md"
}

